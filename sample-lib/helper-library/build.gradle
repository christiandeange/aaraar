plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'maven-publish'
}

group = "sh.christian.samplelib"
version = "1.0.0"

android {
  namespace 'sh.christian.samplelib.helper'
  compileSdk 33

  defaultConfig {
    minSdk 24
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  packagingOptions {
    exclude "**/module-info.class"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = '11'
  }
}

dependencies {
  api fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.0'
}

tasks.register('androidSourcesJar', Jar) {
  archiveClassifier.set('sources')
  from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
  artifacts {
    archives androidSourcesJar
  }

  publishing {
    publications {
      maven(MavenPublication) {
        from components.release
        artifact androidSourcesJar
      }
    }
  }
}
