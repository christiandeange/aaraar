plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'sh.christian.aaraar' version '0.0.12'
  id 'maven-publish'
}

group = "sh.christian.samplelib"
version = "1.0.0"

android {
  namespace 'sh.christian.samplelib'
  compileSdk 33

  defaultConfig {
    minSdk 24
    consumerProguardFiles 'consumer-rules.pro'
  }

  buildTypes {
    release {
      minifyEnabled false
    }

    publish {
      initWith release
      matchingFallbacks = ['release']
    }
  }

  packagingOptions {
    resources.excludes += "**/module-info.class"
    resources.excludes += "exclude-me.txt"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = '11'
  }

  publishing {
    singleVariant("publish") {
      withSourcesJar()
    }
  }
}

dependencies {
  api 'org.jetbrains.kotlin:kotlin-stdlib:1.8.0'

  compileOnly project(':helper-library')
  embed project(':helper-library')

  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'androidx.appcompat:appcompat:1.6.0'
  implementation 'com.google.android.material:material:1.7.0'

  compileOnly 'io.reactivex.rxjava3:rxjava:3.1.5'
  embed 'io.reactivex.rxjava3:rxjava:3.1.5'

  embed fileTree(dir: "libs", include: ["*.jar"])
}

aaraar {
  shading {
    forFiles(fileTree(dir: "libs")) {
      it.addPrefix "shaded."
    }
    forGroup("io.reactivex.rxjava3") {
      it.addPrefix "shaded."
    }
    all {
      it.rename "sh.christian.samplelib.ContextsKt", "sh.christian.samplelib.MyAppPackage"
      it.delete "sh.christian.helperlib.SecretsKt"
    }
  }

  isEnabledForVariant { variant ->
    variant.name == "publish"
  }

  keepMetaFiles = false
}

afterEvaluate {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.publish
      }
    }
  }
}
