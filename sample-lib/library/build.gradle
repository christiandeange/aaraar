plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'sh.christian.aaraar' version '0.0.4'
  id 'maven-publish'
}

group = "sh.christian.samplelib"
version = "1.0.0"

android {
  namespace 'sh.christian.samplelib'
  compileSdk 33

  defaultConfig {
    minSdk 24
    consumerProguardFiles 'consumer-rules.pro'
  }

  buildTypes {
    release {
      minifyEnabled false
    }

    publish {
      initWith release
      matchingFallbacks = ['release']
    }
  }

  packagingOptions {
    exclude "**/module-info.class"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = '11'
  }
}

configurations {
  uploadableArtifact
}

dependencies {
  uploadableArtifact project(path: path, configuration: 'publishEmbedAar')
}

tasks.register("printPublishableAarPath") {
  it.inputs.files configurations.named('uploadableArtifact')

  doLast {
    println("AAR to upload: ${inputs.files.files}")
  }
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.0'

  compileOnly project(':helper-library')
  embed project(':helper-library')

  compileOnly 'androidx.core:core-ktx:1.9.0'
  embed 'androidx.core:core-ktx:1.9.0'

  compileOnly 'androidx.appcompat:appcompat:1.6.0'
  embed 'androidx.appcompat:appcompat:1.6.0'

  compileOnly 'com.google.android.material:material:1.7.0'
  embed 'com.google.android.material:material:1.7.0'

  implementation 'io.reactivex.rxjava3:rxjava:3.1.5'
  embed 'io.reactivex.rxjava3:rxjava:3.1.5'
}

aaraar {
  rename("io.reactivex.rxjava3.**", "shaded.io.reactivex.rxjava3.@1")
  rename("sh.christian.samplelib.ContextsKt", "sh.christian.samplelib.MyAppPackage")
  delete("sh.christian.helperlib.SecretsKt")
}

afterEvaluate {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.publishEmbedAar
      }
    }
  }
}
